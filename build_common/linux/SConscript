##
# This script set linux specific flags (GNU GCC)
#
##
import os
Import('env')

print "Reading linux configuration script"

SConscript('../external_libs.scons')
help_vars = Variables()
if env.get('BUILD_JAVA') == 'ON':
    if not env.get('ANDROID_GRADLE'):
        SConscript('../../extlibs/android/gradle/SConscript')
        help_vars.Add(PathVariable('ANDROID_GRADLE', 'Android Gradle directory', os.path.join(env.get('SRC_DIR'), 'extlibs', 'android', 'gradle', 'gradle-2.2.1/bin/gradle')))

    if env.get('ANDROID_GRADLE'):
        android_gradle = env.get('ANDROID_GRADLE')
    else:
        print '''
*************************************** Info **********************************
*    Android Gradle path isn't set, the default will be used. You can set     *
* environment variable ANDROID_GRADLE or add it in command line as:           *
*      # scons ANDROID_GRADLE=<path to android GRADLE> ...                    *
*******************************************************************************
'''
        android_gradle = os.path.join(env.get('SRC_DIR'), 'extlibs', 'android', 'gradle', 'gradle-2.2.1', 'bin', 'gradle')

help_vars.Update(env)
Help(help_vars.GenerateHelpText(env))


# Set release/debug flags
if env.get('RELEASE'):
	env.AppendUnique(CCFLAGS = ['-Os'])
	env.AppendUnique(CPPDEFINES = ['NDEBUG'])
else:
	env.AppendUnique(CPPFLAGS = ['-g', '--coverage'])
	# --coverage = -fprofile-arcs -ftest-coverage (when compiling) and -lgcov (when linking). 
        env.PrependUnique(LIBS = ['gcov'])

if env.get('LOGGING'):
	env.AppendUnique(CPPDEFINES = ['-DTB_LOG'])

env.AppendUnique(CPPDEFINES = ['WITH_POSIX', '__linux__'])
env.AppendUnique(CPPFLAGS = ['-Wall', '-Wextra', '-fPIC'])
env.AppendUnique(CFLAGS = ['-std=c99'])
env.AppendUnique(CXXFLAGS = ['-Wnoexcept', '-std=c++0x'])
# env.AppendUnique(LIBS = ['uuid'])
env.AppendUnique(LINKFLAGS = ['-ldl', '-lpthread'])

# env.AppendUnique(CFLAGS = ['-ffunction-sections', '-fdata-sections'])

if env.get('WITH_TCP'):
    env.AppendUnique(CPPDEFINES = ['WITH_TCP'])

# Set arch flags that match best TARGET_ARCH variable
target_arch = env.get('TARGET_ARCH')
if target_arch in ['x86']:
	env.AppendUnique(CCFLAGS = ['-m32'])
	env.AppendUnique(LINKFLAGS = ['-m32'])
elif target_arch in ['x86_64']:
	env.AppendUnique(CCFLAGS = ['-m64'])
	env.AppendUnique(LINKFLAGS = ['-m64'])
elif target_arch in ['arm'] or target_arch.find('v5') > 0:
	env.AppendUnique(CPPFLAGS = ['-march=armv5te'])
elif target_arch.find('v7a-hard') > 0:
	env.AppendUnique(CPPFLAGS = ['-march=armv7-a'])
	env.AppendUnique(CPPFLAGS = ['-mfloat-abi=hard'])
	env.AppendUnique(CCFLAGS = ['-mfloat-abi=hard'])
	env.AppendUnique(LINKFLAGS = ['-mfloat-abi=hard'])
elif target_arch.find('v7a') > 0:
	env.AppendUnique(CPPFLAGS = ['-march=armv7-a'])
elif target_arch.find('arm64') >= 0:
	env.AppendUnique(CPPFLAGS = ['-march=armv8-a'])
