#******************************************************************
#
# Copyright 2014 Intel Mobile Communications GmbH All Rights Reserved.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#      http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
#-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=-=

##
# Examples build script
##
Import('env')
# Add third party libraries
lib_env = env.Clone()
SConscript(env.get('SRC_DIR') + '/resource/third_party_libs.scons', 'lib_env')

jni_env = lib_env.Clone()

src_dir   = env.get('SRC_DIR')

######################################################################
# Build flags
######################################################################

jni_env.AppendUnique(CPPPATH = [
		src_dir + '/resource/csdk/connectivity/api',
		src_dir + '/resource/include',
		src_dir + '/resource/c_common',
		src_dir + '/resource/c_common/oic_string/include',
		src_dir + '/resource/c_common/oic_malloc/include',
		src_dir + '/resource/csdk/stack/include',
		src_dir + '/resource/csdk/ocsocket/include',
		src_dir + '/resource/oc_logger/include',
		src_dir + '/resource/csdk/logger/include',
		src_dir + '/resource/../extlibs/boost/boost_1_58_0',
		src_dir + '/resource/../build_common/android/compatibility',
		src_dir + '/resource/csdk/security/provisioning/include',
		src_dir + '/resource/csdk/security/provisioning/include/oxm',
		src_dir + '/resource/csdk/security/provisioning/include/internal',
		src_dir + '/resource/csdk/security/include'
		])

target_os = env.get('TARGET_OS')

if target_os == 'darwin':
    jni_env.Replace(SHLIBSUFFIX = '.jnilib')

jni_env.AppendUnique(CCFLAGS = ['-Wno-error', '-Wno-comment', '-Wno-unused-function', '-Wno-unused-parameter'])
if target_os not in ['windows', 'winrt']:
	jni_env.AppendUnique(CXXFLAGS = ['-pthread'])

	# Note: 'pthread' is in libc for android. On other platform, if use
	# new gcc(>4.9?) it isn't required, otherwise, it's required
	if target_os != 'android':
		jni_env.AppendUnique(LIBS = ['-lpthread'])

jni_env.AppendUnique(LIBPATH = [env.get('BUILD_DIR')])
jni_env.AppendUnique(RPATH = [env.get('BUILD_DIR')])
jni_env.PrependUnique(LIBS = ['oc', 'octbstack', 'oc_logger', 'connectivity_abstraction'])
if env.get('SECURED') == '1':
    jni_env.AppendUnique(LIBS = ['tinydtls', 'ocprovision', 'ocpmapi'])

if target_os == 'android':
	jni_env.AppendUnique(CXXFLAGS = ['-frtti', '-fexceptions'])
	jni_env.AppendUnique(LIBS = ['gnustl_shared'])

	if not env.get('RELEASE'):
		jni_env.AppendUnique(LIBS = ['log'])

if target_os in ['darwin', 'ios']:
	jni_env.AppendUnique(CPPDEFINES = ['_DARWIN_C_SOURCE'])

######################################################################
# Source files and Targets
######################################################################

print jni_env.Dump()

ocstack_files = [
			'JniOcStack.cpp',
			'JniUtils.cpp',
			'JniEntityHandler.cpp',
			'JniOnResourceFoundListener.cpp',
			'JniOnDeviceInfoListener.cpp',
			'JniOnPlatformInfoListener.cpp',
			'JniOnPresenceListener.cpp',
			'JniOnGetListener.cpp',
			'JniOnPutListener.cpp',
			'JniOnPostListener.cpp',
			'JniOnDeleteListener.cpp',
			'JniOnObserveListener.cpp',
			'JniOcRepresentation.cpp',
			'JniOcResourceHandle.cpp',
			'JniOcPresenceHandle.cpp',
			'JniOcRequestHandle.cpp',
			'JniOcResourceRequest.cpp',
			'JniOcResourceResponse.cpp',
			'JniOcPlatform.cpp',
			'JniOcResource.cpp',
			'JniOcResourceIdentifier.cpp',
			'JniOcSecurity.cpp'
		]
if env.get('SECURED') == '1':
	ocstack_files += [
				'JniOcSecureResource.cpp',
				'JniOcProvisioning.cpp',
				'JniSecureUtils.cpp',
				'JniProvisionResultListner.cpp',
				'JniPinCheckListener.cpp',
				'JniDisplayPinListener.cpp'
			]

ca_interface = jni_env.SharedLibrary(target = 'ca-interface',
	       			source = ['JniCaInterface.c'])

ocstack_jni = jni_env.SharedLibrary(target = 'ocstack-jni',
	      			source = ocstack_files)

jni_env.InstallTarget(ocstack_jni, 'libocstack-jni')
jni_env.InstallTarget(ca_interface, 'libca-interface')

env.AppendTarget('ocstack-jni')
